plugins {
    id 'java-gradle-plugin'
    id "com.diffplug.spotless" version "6.11.0"
//    id 'pt.com.hugo-dias.git-versioner' version '1.0.0'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "1.0.0"
}

group 'pt.com.hugodias.gradle.gitversioner'
version '0.0.2'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly gradleApi()
    compileOnly 'org.projectlombok:lombok:1.18.24'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.3.0.202209071007-r'
    implementation "com.jcraft:jsch:0.1.55"
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation('commons-io:commons-io:2.11.0')
    testImplementation('org.assertj:assertj-core:3.23.1')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.9.0')
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'pt.com.hugo-dias.git-versioner'
            displayName = 'Automated semantic versioning'
            description = 'This plugin will help you by creating a new version for each git commit based in the commit description'
            implementationClass = 'pt.com.hugodias.gradle.gitversioner.VersionerPlugin'
        }
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        formatAnnotations()

        licenseHeader '/* (C) 2022 Hugo Dias */'
    }
}

pluginBundle {
    website = 'https://github.com/AnakinPt/git-versioner'
    vcsUrl = 'https://github.com/AnakinPt/git-versioner'
    tags = ['git', 'version', 'semantic-version']
}

tasks.withType(Javadoc) {
    enabled = false
}

publishing {
    repositories {
        maven {
            name = 'localPluginRepository'
            url = '../local-plugin-repository'
        }
    }
}